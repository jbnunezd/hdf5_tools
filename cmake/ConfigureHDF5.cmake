#--------------------------------------------------------------------------------------------------#
# Build HDF5 library
#--------------------------------------------------------------------------------------------------#
IF(NOT MAIN_BUILD_HDF5)
  FIND_PACKAGE(HDF5 REQUIRED COMPONENTS C Fortran)
  IF (HDF5_FOUND)
    INCLUDE_DIRECTORIES(${HDF5_INCLUDE_DIR})
    LIST(APPEND HDF5_LIBRARIES ${HDF5_hdf5_fortran_LIBRARY_RELEASE})
    LIST(APPEND HDF5_LIBRARIES ${HDF5_hdf5_LIBRARY_RELEASE})
    LIST(APPEND HDF5_LIBRARIES ${HDF5_z_LIBRARY_RELEASE})
    LIST(APPEND HDF5_LIBRARIES -ldl)
    LIST(APPEND LINKEDLIBS ${HDF5_LIBRARIES})
    MESSAGE(STATUS "HDF5 Library: " ${HDF5_LIBRARIES})
  ELSE()
    SET(MAIN_BUILD_HDF5 ON)
  ENDIF()
ENDIF()

IF(MAIN_BUILD_HDF5)
  INCLUDE(ExternalProject)
  SET(MAIN_HDF5_BASE ${MAIN_EXTERNAL_LIB_DIR}/HDF5)
  SET(MAIN_HDF5_VERSION "1.12.1")
  SET(MAIN_HDF5_MAJVER "1")
  SET(MAIN_HDF5_MINVER "12")
  SET(MAIN_HDF5_PAGE "https://support.hdfgroup.org/ftp/HDF5/releases")
  SET(MAIN_HDF5_SUBD "hdf5-${MAIN_HDF5_MAJVER}.${MAIN_HDF5_MINVER}/hdf5-${MAIN_HDF5_VERSION}/src")
  SET(MAIN_HDF5_FILE "hdf5-${MAIN_HDF5_VERSION}.tar.gz")
  SET(MAIN_HDF5_LINK "${MAIN_HDF5_PAGE}/${MAIN_HDF5_SUBD}/${MAIN_HDF5_FILE}")
  
  SET(MAIN_HDF5_SOURCE ${MAIN_EXTERNAL_LIB_SOURCE_DIR}/${MAIN_HDF5_FILE})
  SET(MAIN_HDF5_UNTAR_COMMAND tar -xzf ${MAIN_HDF5_SOURCE} -C ${MAIN_HDF5_BASE})
  IF (NOT EXISTS "${MAIN_HDF5_SOURCE}")
    SET(MAIN_HDF5_DOWNLOAD_COMMAND wget -N ${MAIN_HDF5_LINK})
    SET(MAIN_HDF5_DOWNLOAD_COMMAND ${MAIN_HDF5_DOWNLOAD_COMMAND} && ${MAIN_HDF5_UNTAR_COMMAND})
  ELSE()
    SET(MAIN_HDF5_DOWNLOAD_COMMAND ${MAIN_HDF5_UNTAR_COMMAND})
  ENDIF()

  IF (NOT EXISTS "${MAIN_HDF5_BASE}/lib/libhdf5.a")
    IF(MAIN_ENABLE_MPI)
      SET(MAIN_HDF5_PARALLEL --enable-parallel)
      SET(MAIN_HDF5_FC  ${MPI_Fortran_COMPILER})
      SET(MAIN_HDF5_CC  ${MPI_C_COMPILER})
      SET(MAIN_HDF5_CXX ${MPI_CXX_COMPILER})
    ELSE()
      UNSET(MAIN_HDF5_PARALLEL)
      SET(MAIN_HDF5_FC  ${CMAKE_Fortran_COMPILER})
      SET(MAIN_HDF5_CC  ${CMAKE_C_COMPILER} )
      SET(MAIN_HDF5_CXX ${CMAKE_CXX_COMPILER} )
    ENDIF()
    SET(MAIN_HDF5_COMPILERS FC=${MAIN_HDF5_FC} CC=${MAIN_HDF5_CC})
    SET(MAIN_HDF5_COMMAND ${MAIN_HDF5_BASE}/hdf5-${MAIN_HDF5_VERSION}/configure)
    SET(MAIN_HDF5_OPTIONS --prefix=${MAIN_HDF5_BASE} --enable-fortran)
    SET(MAIN_HDF5_OPTIONS ${MAIN_HDF5_OPTIONS} ${MAIN_HDF5_PARALLEL} --disable-shared)
    SET(MAIN_HDF5_CONFIGURE ${MAIN_HDF5_COMPILERS} ${MAIN_HDF5_COMMAND} ${MAIN_HDF5_OPTIONS})
    ExternalProject_Add(HDF5
      PREFIX     ${MAIN_HDF5_BASE}
      SOURCE_DIR ${MAIN_HDF5_BASE}
      BINARY_DIR ${MAIN_HDF5_BASE}/build
      STAMP_DIR  ${MAIN_HDF5_BASE}/stamp
      TMP_DIR    ${MAIN_HDF5_BASE}/tmp
      DOWNLOAD_DIR ${MAIN_EXTERNAL_LIB_SOURCE_DIR}
      DOWNLOAD_COMMAND ${MAIN_HDF5_DOWNLOAD_COMMAND}
      UPDATE_COMMAND ""
      CONFIGURE_COMMAND ${MAIN_HDF5_CONFIGURE}
      BUILD_COMMAND ${MAKE}
    )
  ENDIF()

  UNSET(HDF5_LIBRARIES)
  UNSET(HDF5_INCLUDE_DIR_FORTRAN)
  UNSET(HDF5_INCLUDE_DIR)
  UNSET(HDF5_DIFF_EXECUTABLE)
  SET(HDF5_hdf5_LIBRARY_RELEASE         ${MAIN_HDF5_BASE}/lib/libhdf5.a)
  SET(HDF5_hdf5_fortran_LIBRARY_RELEASE ${MAIN_HDF5_BASE}/lib/libhdf5_fortran.a)
  SET(HDF5_C_INCLUDE_DIR                ${MAIN_HDF5_BASE}/include)
  SET(HDF5_Fortran_INCLUDE_DIR          ${MAIN_HDF5_BASE}/include)
  SET(HDF5_DIFF_EXECUTABLE              ${MAIN_HDF5_BASE}/bin/h5diff)
  LIST(APPEND HDF5_INCLUDE_DIR          ${HDF5_C_INCLUDE_DIR})
  LIST(APPEND HDF5_INCLUDE_DIR_FORTRAN  ${HDF5_Fortran_INCLUDE_DIR} ${HDF5_C_INCLUDE_DIR})
  LIST(APPEND HDF5_LIBRARIES ${HDF5_hdf5_fortran_LIBRARY_RELEASE})
  LIST(APPEND HDF5_LIBRARIES ${HDF5_hdf5_LIBRARY_RELEASE})
  LIST(APPEND HDF5_LIBRARIES -lz -ldl)
  LIST(APPEND LINKEDLIBS ${HDF5_LIBRARIES})
  INCLUDE_DIRECTORIES(${HDF5_INCLUDE_DIR})
  MESSAGE(STATUS "HDF5 Library: " ${HDF5_LIBRARIES})
ENDIF() 
#--------------------------------------------------------------------------------------------------#
